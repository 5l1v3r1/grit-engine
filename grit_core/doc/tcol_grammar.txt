===========================
HUMAN-READABLE TCOL GRAMMAR
===========================

Key:
"<THIS>" is a token
"This" is a call to a factored out structure which should also be defined in this file
"|" is the choice between several options
"[]" is an optional sequence
"{}" is a sequence that can be repeated 0 or more times


File ::= <TCOL> [ <ATTRIBUTES> <LBRACE> {Attribute} <RBRACE> ]
         ( CompoundShape [ TriMesh ] | TriMesh )

Attribute ::= <STATIC> <SEMICOLON>
            | <MASS> <FLOAT> <SEMICOLON>
            | <INERTIA> <FLOAT> <FLOAT> <FLOAT> <SEMICOLON>
            | <FRICTION> <FLOAT> <SEMICOLON>
            | <RESTITUTION> <FLOAT> <SEMICOLON>
            | <LINEAR_DAMPING> <FLOAT> <SEMICOLON>
            | <ANGULAR_DAMPING> <FLOAT> <SEMICOLON>
            | <LINEAR_SLEEP_THRESHOLD> <FLOAT> <SEMICOLON>
            | <ANGULAR_SLEEP_THRESHOLD> <FLOAT> <SEMICOLON>
            | <CCD_MOTION_THRESHOLD> <FLOAT> <SEMICOLON>
            | <CCD_SWEPT_SPHERE_RADIUS> <FLOAT> <SEMICOLON>


CompoundShape ::= <COMPOUND> <LBRACE> { InternalShape } <RBRACE>

InternalShape ::= <HULL> <LBRACE> [ Margin ] [Shrink] [ Material ] Vertexes <RBRACE>
                | <BOX> <LBRACE> [ Margin ] [ Material ] Centre [ Orientation ] Dimensions <RBRACE>
                | <CYLINDER> <LBRACE> [ Margin ] [ Material ] Centre [ Orientation ] Dimensions <RBRACE>
                | <CONE> <LBRACE> [ Margin ] [ Material ] Centre [ Orientation ] Radius Height <RBRACE>
                | <PLANE> <LBRACE> [ Material ] Normal Distance <RBRACE>
                | <SPHERE> <LBRACE> [ Material ] Centre Radius <SEMICOLON> <RBRACE>

Margin ::= <MARGIN> <FLOAT> <SEMICOLON>

Shrink ::= <SHRINK> <FLOAT> <SEMICOLON>

Vertexes ::= <VERTEXES>
             <LBRACE> { <FLOAT> <FLOAT> <FLOAT> <SEMICOLON> } <RBRACE>

Centre ::= <CENTRE> <FLOAT> <FLOAT> <FLOAT> <SEMICOLON>

Orientation ::= <ORIENTATION> <FLOAT> <FLOAT> <FLOAT> <SEMICOLON>

Dimensions ::= <DIMENSIONS> <FLOAT> <FLOAT> <FLOAT> <SEMICOLON>

Radius ::= <RADIUS> <FLOAT> <SEMICOLON>

Height ::= <HEIGHT> <FLOAT> <SEMICOLON>

Normal ::= <NORMAL> <FLOAT> <FLOAT> <FLOAT> <SEMICOLON>

Distance ::= <DISTANCE> <FLOAT> <SEMICOLON>

Material ::= <MATERIAL> <HEX> <SEMICOLON>


TriMesh ::= <TRIMESH> <LBRACE> [ Margin ] Vertexes Faces <RBRACE>

Faces ::= <FACES> <LBRACE>
              { <NATURAL> <NATURAL> <NATURAL> [ <HEX> ] <SEMICOLON> }
          <RBRACE>
